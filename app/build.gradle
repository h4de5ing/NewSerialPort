plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

static def gitSha() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Exception ignored) {
        return 'unknown'
    }
}

android {
    namespace 'com.android.serialport2'
    compileSdk 34

    defaultConfig {
        applicationId "com.android.serialport2"
        minSdk 24
        targetSdk 34
        versionCode 100
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        flavorDimensions = ["default"]
        buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        //noinspection ChromeOsAbiSupport
        ndk { abiFilters 'armeabi' }
    }
    signingConfigs {
        normal {
            storeFile file("../app.jks")
            storePassword('android')
            keyAlias('android')
            keyPassword('android')
        }
        system {
            storeFile file("D:\\Android12SignerGUI\\SignFiles\\NewPublic\\platform.jks")
            storePassword('android')
            keyAlias('android')
            keyPassword('android')
        }
    }

    productFlavors {
        nromal {
            applicationId = 'com.android.serialport2'
            manifestPlaceholders = [sharedUserId    : "com.android.serialport2",
                                    applicationLabel: "SerialPort",
                                    applicationIcon : "@mipmap/ic_launcher"]
            signingConfig signingConfigs.normal
        }
        system {
            applicationId = 'com.android.serialport2.system'
            manifestPlaceholders = [sharedUserId    : "android.uid.system",
                                    applicationLabel: "SystemSerialPort",
                                    applicationIcon : "@mipmap/ic_launcher_system"]
            signingConfig signingConfigs.system
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.normal
//            signingConfig signingConfigs.system
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }
    kotlinOptions { jvmTarget = '19' }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
    applicationVariants.configureEach { variant ->
        def buildType = variant.buildType.name
        def prefix = "android_serialport"
        def createTime = new Date().format("yyyyMMddHHmmss")
        if (buildType == "release") {
            variant.outputs.each {
                def fromFile = it.outputFile
                def intoFile = "D:/test/${prefix}/v${android.defaultConfig.versionName}/"
                copy {
                    from fromFile
                    into intoFile
                    include('*.apk')
                    rename('app-', "${prefix}_v${android.defaultConfig.versionName}_${createTime}_${gitSha()}_")
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform("androidx.compose:compose-bom:2023.08.00")
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material3:material3-window-size-class"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation "com.google.accompanist:accompanist-adaptive:0.26.2-beta"
}