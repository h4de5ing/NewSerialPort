plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

static def gitSha() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Exception ignored) {
        return 'unknown'
    }
}

android {
    namespace 'com.android.serialport2'
    compileSdk 34

    defaultConfig {
        applicationId "com.android.serialport2"
        minSdk 24
        targetSdk 34
        versionCode 500
        versionName "5.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        flavorDimensions = ["default"]
        buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        //noinspection ChromeOsAbiSupport
        ndk { abiFilters 'armeabi' }
    }
    signingConfigs {
        normal {
            storeFile file("../app.jks")
            storePassword('android')
            keyAlias('android')
            keyPassword('android')
        }
        system {
            storeFile file("D:\\Android12SignerGUI\\SignFiles\\NewPublic\\platform.jks")
            storePassword('android')
            keyAlias('android')
            keyPassword('android')
        }
    }

    productFlavors {
        normal {
            applicationId = 'com.android.serialport2'
            manifestPlaceholders = [sharedUserId    : "com.android.serialport2",
                                    applicationLabel: "SerialPort",
                                    applicationIcon : "@mipmap/ic_launcher"]
            signingConfig signingConfigs.normal
        }
        system {
            applicationId = 'com.android.serialport2.system'
            manifestPlaceholders = [sharedUserId    : "android.uid.system",
                                    applicationLabel: "SystemSerialPort",
                                    applicationIcon : "@mipmap/ic_launcher_system"]
//            signingConfig signingConfigs.system
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
//            signingConfig signingConfigs.normal
            signingConfig signingConfigs.system
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }
    kotlinOptions {
        jvmTarget = '19'
        // Enable Compose Compiler Report
        freeCompilerArgs += ["-P", "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" + project.buildDir.absolutePath + "/compose_metrics"]
        // Enable Compose Compiler Metrics
        freeCompilerArgs += ["-P", "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" + project.buildDir.absolutePath + "/compose_metrics"]
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
            proto { srcDir 'src/main/proto' }
        }
    }
    applicationVariants.configureEach { variant ->
        def buildType = variant.buildType.name
        def prefix = "android_serialport"
        def createTime = new Date().format("yyyyMMddHHmmss")
        if (buildType == "release") {
            variant.outputs.each {
                def fromFile = it.outputFile
                def intoFile = "D:/test/${prefix}/v${android.defaultConfig.versionName}/"
//                def intoFile = "\\\\192.168.0.74\\亿道信息\\RD\\SW_A\\71备份过来的\\APP软件版本\\串口工具\\v${android.defaultConfig.versionName}"
                copy {
                    from fromFile
                    into intoFile
                    include('*.apk')
                    rename('app-', "${prefix}_v${android.defaultConfig.versionName}_${createTime}_${gitSha()}_")
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0-alpha01'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.7.0-alpha01"
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform("androidx.compose:compose-bom:2023.08.00")
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3:1.2.0-alpha06'
    implementation 'androidx.compose.material3:material3-window-size-class:1.2.0-alpha06'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0-alpha01'
    implementation 'androidx.compose.runtime:runtime-livedata:1.6.0-alpha04'
    implementation 'com.google.accompanist:accompanist-adaptive:0.33.0-alpha'
    implementation 'androidx.datastore:datastore:1.0.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.19.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'
}
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.14.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}